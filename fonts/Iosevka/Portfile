# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0

categories          fonts
github.setup        be5invis Iosevka 15.3.1 v
revision            0

description         Slender typeface for code, from code
long_description \
    ${name} is an open-source, sans-serif + slab-serif, monospace + \
    quasiâ€‘proportional typeface family, designed for writing code, using in \
    terminals, and preparing technical documents.
homepage            https://typeof.net/Iosevka/

platforms           darwin
supported_archs     noarch
maintainers         {amake @amake} openmaintainer
license             OFL

set plan            iosevka-custom

patch {
    copy ${filespath}/private-build-plans.toml ${worksrcpath}
}

variant precompiled description {Use precompiled binaries} conflicts min_leading {
    global plan
    set plan        iosevka

    github.tarball_from releases

    distname        super-ttc-iosevka-${github.version}
    use_configure   no
    use_zip         yes
    worksrcdir      .

    checksums       rmd160  c0ec4acec90ae801c4dd943134b235799dbe90d9 \
                    sha256  553b3e1da4a35396993a755b952b573ec466c4f47a6b4efa07e0636cd65b2600 \
                    size    64300887

    build {}

    global workbinglob
    set workbinglob ${worksrcpath}/*.ttc
}

variant min_leading description {Build with minimal line height} conflicts precompiled {
    post-patch {
        reinplace "s|# leading = 1250|leading = 1110|" ${worksrcpath}/private-build-plans.toml
    }
}

variant ss08 description {Pragmata Pro Style} {
    if {[variant_isset precompiled]} {
        distname    super-ttc-iosevka-ss08-${github.version}

        checksums   rmd160  71b1d685bdd349b8e0c2ed978f78df0e7f223cff \
                    sha256  a273f23a59ac6c8da58f933d91664786845339a10d3181b78380987cca5aa4d3 \
                    size    19028808
    }

    post-patch {
        reinplace "s|# inherits = \"ss01\"|inherits = \"ss08\"|" ${worksrcpath}/private-build-plans.toml
    }
}

default_variants    +precompiled

if {![variant_isset precompiled]} {
    depends_build-append \
        bin:npm:npm8 \
        port:ttfautohint

    checksums       rmd160  5ad0b70fdbf3baffd5fa013eca175870c7a870b0 \
                    sha256  a69f5499affc0602379fe9de97ace7a4fe5014f58bfd954b6958015222085b07 \
                    size    17452886

    configure {
        system -W ${worksrcpath} "npm install"
    }

    build {
        system -W ${worksrcpath} "TERM=dumb npm run build -- --verbosity=8 ttf::${plan}"
    }

    pre-destroot {
        # Do this late because otherwise we won't see the value overwritten by
        # variants, since this is in the "main" scope(?)
        global workbinglob
        set workbinglob ${worksrcpath}/dist/${plan}/ttf/*
    }
}

destroot.violate_mtree yes

destroot {
    set target ${destroot}/Library/Fonts/
    xinstall -m 0755 -d ${target}
    copy {*}[glob ${workbinglob}] ${target}
}

github.livecheck.regex  {([^"-]+)}
